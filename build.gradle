buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

subprojects {
    apply plugin: "java"
    buildscript {
        repositories.mavenLocal()
    }

    dependencies {
        implementation "com.google.truth:truth:0.44"
    }

    apply plugin: 'maven-publish'

    group = 'com.google.archivepatcher'
    version = '2.0.0'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "../build/repos/releases"
                def snapshotsRepoUrl = "../build/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
